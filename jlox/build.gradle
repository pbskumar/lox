plugins {
    id 'java'
    id 'application'
}

group = 'com.craftinginterpreters'
version = '1.0'

application {
    mainClass = 'com.craftinginterpreters.lox.Lox'
}

def generatedSrcDir = layout.buildDirectory.dir("generated-src/main/java")

sourceSets {
    common {
        java.srcDir('src/common/java')
    }
    tool {
        java.srcDir('src/tool/java')
        compileClasspath += sourceSets.common.output
        runtimeClasspath += sourceSets.common.output
    }
    main {
        java {
            srcDir("src/main/java")
            srcDir(generatedSrcDir)
        }
        compileClasspath += sourceSets.common.output
        runtimeClasspath += sourceSets.common.output
    }
}

tasks.register('generateAst', JavaExec) {
    group = "code generation"
    description = "Generates AST classes for Lox based on the BNF grammar"

    classpath = sourceSets.tool.runtimeClasspath
    mainClass = 'com.craftinginterpreters.tool.GenerateAst'

    args(generatedSrcDir.get().asFile.absolutePath)
    outputs.dir(generatedSrcDir)

}

tasks.named('compileJava') {
    dependsOn(tasks.named('generateAst'))
}

tasks.named("run") {
    doFirst {
        logger.lifecycle("Starting jlox REPL (Ctrl-D to exit)")
    }
    standardInput = System.in
}

tasks.named('jar') {
    from(sourceSets.common.output)
}

// Task to generate a temporary PATH script
tasks.register('addDistScript') {
    group = "run"
    description = "Generates a script to temporarily add jlox to PATH"

    doLast {
        def scriptFile = layout.buildDirectory.file("jlox-env.sh").get().asFile
        def distBin = layout.buildDirectory.dir("install/jlox/bin").get().asFile

        scriptFile.text = "export PATH=${distBin}:\$PATH"
        println "Temporary PATH script generated: ${scriptFile}"
        println "Run `source ${scriptFile}` to add jlox to your PATH for this shell session."
    }
}

// Make build automatically install the distribution
tasks.build {
    dependsOn tasks.installDist
    finalizedBy tasks.addDistScript
}


tasks.named('clean') {
    delete(layout.buildDirectory.dir("generated-src"))
}
